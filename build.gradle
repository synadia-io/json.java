import aQute.bnd.gradle.Bundle

plugins {
    id("java-library")
    id("maven-publish")
    id("jacoco")
    id("biz.aQute.bnd.builder") version "7.2.1"
    id("org.gradle.test-retry") version "1.6.4"
    id("io.github.gradle-nexus.publish-plugin") version "2.0.0"
    id("signing")
}

def jarVersion = "3.0.1"
group = 'io.synadia'

def isRelease = System.getenv("BUILD_EVENT") == "release"
def tc = System.getenv("TARGET_COMPATIBILITY")
def targetCompat = tc == "25" ? JavaVersion.VERSION_25 : JavaVersion.VERSION_21
def jarEnd =       tc == "25" ?  "-jdk25"              : ""
def jarAndArtifactName = "jnats-json" + jarEnd

version = isRelease ? jarVersion : jarVersion + "-SNAPSHOT" // version is the variable the build actually uses.

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = targetCompat
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
    maven { url="https://repo1.maven.org/maven2/" }
    maven { url="https://central.sonatype.com/repository/maven-snapshots/" }
}

dependencies {
    implementation 'org.jspecify:jspecify:1.0.0'
    implementation 'commons-codec:commons-codec:1.20.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.14.1'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.register('bundle', Bundle) {
    from sourceSets.main.output
}

jar {
    bundle {
        bnd("Bundle-Name": "io.synadia.jnats.json",
                "Bundle-Vendor": "synadia.io",
                "Bundle-Description": "JNats JSON Utility",
                "Bundle-DocURL": "https://github.com/synadia-io/jnats.json",
                "Target-Compatibility": "Java " + targetCompat
        )
    }
}

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
        events "started", "passed", "skipped", "failed"
        showStandardStreams = true
    }
}

javadoc {
    failOnError = false
    options.overview = 'src/main/javadoc/overview.html' // relative to source root
    source = sourceSets.main.allJava
    title = "JNats JSON Utility"
    classpath = sourceSets.main.runtimeClasspath
}

jacoco {
    toolVersion = "0.8.13"
}

jacocoTestReport {
    reports {
        xml.required = true // coveralls plugin depends on xml format report
        html.required = true
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
            username = System.getenv('OSSRH_USERNAME')
            password = System.getenv('OSSRH_PASSWORD')
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'JNats JSON Utility'
                description = 'JSON Parser originally built for JNATS'
                url = 'https://github.com/synadia-io/jnats.json'

                groupId = group
                artifactId = jarAndArtifactName

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'synadia'
                        name = 'Synadia'
                        email = 'info@synadia.com'
                        url = 'https://synadia.io'
                    }
                }
                scm {
                    url = 'https://github.com/synadia-io/jnats.json'
                }
            }
        }
    }
}

if (isRelease) {
    signing {
        def signingKeyId = System.getenv('SIGNING_KEY_ID')
        def signingKey = System.getenv('SIGNING_KEY')
        def signingPassword = System.getenv('SIGNING_PASSWORD')
        useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)
        sign configurations.archives
        sign publishing.publications.mavenJava
    }
}
